SEED             = args[1]
TARGET_ALN       = args[2]
TARGET_DIRECTORY = args[3]

seed(SEED)

### Read in sequence data for the gene
data = readDiscreteCharacterData(TARGET_ALN)

# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()


mvi = 1
mni = 1


######################
# Substitution Model #
######################

# specify the stationary frequency parameters
pi_prior <- v(1,1,1,1)
pi ~ dnDirichlet(pi_prior)

# specify the exchangeability rate parameters
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)

# create a deterministic variable for the rate matrix, GTR
Q := fnGTR(er,pi)


# among-site rate variation
alpha ~ dnHalfCauchy(0,1)

gamma_cats := fnDiscretizeGamma(alpha,alpha,4)

##############
# Tree model #
##############

# Prior distribution on the tree topology
psi ~ dnUniformTopologyBranchLength(taxa, dnExponential(10))

###################
# PhyloCTMC Model #
###################

# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, siteRates=gamma_cats, type="DNA")

# attach the data
seq.clamp(data)

###################################
# Posterior Predictive Simulation #
###################################

mymodel = model(psi)

trace = readStochasticVariableTrace(TARGET_DIRECTORY + "/analysis.log", delimiter=TAB)

pps = posteriorPredictiveSimulation(mymodel, directory=TARGET_DIRECTORY + "/PPS", trace)

pps.run(thinning=4)

q()
